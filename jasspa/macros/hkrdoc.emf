; -!- emf -!-
; This is part of the JASSPA MicroEmacs macro files
; Copyright (C) 1999-2009 JASSPA (www.jasspa.com)
; See the file me.emf for copying and conditions.
;
; Created:     Oct 7 2022
; Authors:     Detlef Groth
; Synopsis:    R-doc file hook (https://cran.r-project.org/doc/manuals/r-release/R-exts.html#Rd-format)
; 

define-macro fhook-rdoc
  @# buffer-init "rdoc"
  buffer-init-hooks
!emacro

; buffer-init variables
set-variable .fhook-rdoc.name "Rdoc"
set-variable .fhook-rdoc.setup &reg "/history/fhook/rdoc" "acfhmw"
set-variable .fhook-rdoc.setup-mask "acfhmw"
;set-variable .fhook-rdoc.comment "|%||%|% | %||"

!if &and &sin "h" .fhook-rdoc.setup &band .hilight.flags 0x02
  !iif &not &exi .hilight.rdoc  set-variable .hilight.rdoc &pinc .hilight.next 1
  !iif &not &exi .hilight.rdocL  set-variable .hilight.rdocL &pinc .hilight.next 1  
  0 hilight .hilight.rdoc 0                 .scheme.text
  0 hilight .hilight.rdocL 0                 .scheme.text  
  hilight .hilight.rdoc 2 "^_"               .scheme.header
  hilight .hilight.rdocL 4 "'" "'" "\\"    .scheme.link
  hilight .hilight.rdoc 2 "^[A-Z]"            .scheme.header
  ;hilight .hilight.rdoc 2 "^ +[a-z]+:"         .scheme.keyword  
  !iif &not &exi ".hilight.r"  !force execute-file "hkr"
  hilight .hilight.rdoc 0x80 "^[A-Z].+"  .hilight.rdoc .scheme.header
  hilight .hilight.rdoc 0x80 "^See Also:"  .hilight.rdocL .scheme.header  
  hilight .hilight.rdoc 0x80 "^Usage:"  .hilight.r .scheme.header
  hilight .hilight.rdoc 0x80 "^Examples:"  .hilight.r .scheme.header  
  hilight .hilight.r 0x80 "^Arguments:"  .hilight.rdoc  .scheme.header
  hilight .hilight.rdocL 0x80 "^Examples:"  .hilight.r  .scheme.header
!endif

!if &sin "f" .fhook-rdoc.setup
  ; setup emf collapsing
  set-variable .fhook-rdoc.collapse-open  "^[A-Z]"
  set-variable .fhook-rdoc.collapse-close "^\\([A-Z]\\|\\'\\)"
  set-variable .fhook-rdoc.collapse-mnext "-1"
!endif

define-macro rdoc
  !force set-variable #l0 @1
  !if &not $status
    set-variable #l0 @ml00 "R help on"
  !endif
  !if &xse #l0 "\\(.+\\)::\\(.+\\)" 
      set-variable #l1 &spr "help('%s',package='%s')" @s2 @s1
      2000 ml-write #l1
  !else
      set-variable #l1 &spr "help('%s')" #l1
  !endif
  0 pipe-shell-command &spr "LANG=en_US Rscript -e \"%s\"" #l1 "*rdoc*"
  set-variable :mouse-word-select rdoc-process-link
  -1 buffer-mode "view"
  beginning-of-buffer
  3 kill-line
  replace-string "_" "" 
  beginning-of-buffer
  1 buffer-mode "view"
  !if &band $system 0x01
    ; some terminals might need this
    screen-update
  !endif
!emacro    

0 define-macro rdoc-process-link
  ;set-variable $debug 3
  set-variable #l0 $window-col
  set-variable #l1 $window-line
  set-position "\x80"
  !force -10 search-backward "^See Also"
  !if $status
    goto-position "\x80"
    !force -2 search-forward "'"
    !if $status
      set-variable #l2 $window-col
      set-variable #l3 $window-line
      backward-char
      !force -2 search-backward "'"
      !if $status
        set-variable #l4 $window-col
        set-variable #l5 $window-line
        ; all on same line?
        !if &and &equ #l1 #l3 &equ #l3 #l5
          !if &and &less #l0 #l2 &great #l0 #l4
            forward-char
            set-mark
            search-forward "'"
            backward-char
            copy-region
            set-variable #l6 @y
            -1 yank
            rdoc #l6
          !endif
        !endif
      !endif
    !endif
  !else
    1000 ml-write "We are not in the See Also section."
    goto-position "\x80"
  !endif  
!emacro

;add-file-hook "*rdoc*" fhook-ehf
buffer-init-fhook "rdoc"
ml-write "hkrdoc loaded"
