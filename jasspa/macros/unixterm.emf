; -!- emf -!-
; This is part of the JASSPA MicroEmacs macro files
; Copyright (C) 1995-2009 JASSPA (www.jasspa.com)
; Copyright (C) 2021-2023 Detlef Groth (https://github.com/mittelmark/microemacs)
; See the file me.emf for copying and conditions.
;
; Synopsis:    Common unix setup file
; Authors:     Steven Phillips, Detlef Groth
;
define-macro-file hkman man
define-macro-file hkipipe ishell gdb dbx perldb
add-file-hook ".C" fhook-cpp
0 define-macro exec-me
    ipipe-shell-command &spr "\"%s\" %s" $progname @1 @mna
!emacro

; TODO check on more platforms
; found it on MacOS Quartz, Ubuntu 18, Fedora 30, Fedora 38
set-variable %font-default "-*-courier-bold-r-*-*-%d-*-*-*-*-*-*"
set-variable %fonts %font-default
set-variable %font-sizes  "|10|12|14|17|20|24|25|34|"
set-variable %font-index 4

!if &band $system 0x01
    ; Not using X
    !if &not &band $system 0x02
        ; no color - change the window chars so osd is more usable
        set-variable $window-chars &spr "%s=x*[>]<%s" &lef $window-chars 47 &rig $window-chars 54
    !endif
    ; suspend only on Terminal without questioning
    global-unbind-key "C-z"
    0 global-bind-key suspend-emacs "C-z"
!else
    ; TODO: fix if the font was deleted!
    ; try xset fp default
    !if &not &seq &set #l0 &reg &spr "/history/%s/font" $platform "" ""
        ; ignore errors with font in registry
        ; should fallback to default font
        ; seems to work
        !force !force change-font #l0
    !endif
    ; might be called like this:
    ; change-font-type "roboto mono-bold" "|10|14|17|19|22|25|28|33|" 25
    define-macro change-font-type
        !if &equal @# 0
            set-variable %font-index 4
        !else
            set-variable %font-index &add %font-index @#
        !endif
        !if &less %font-index 1
            set-variable %font-index 1
        !endif
        set-variable #l0 &lget %font-sizes %font-index
        !if &seq #l0 ""
            set-variable %font-index &add %font-index -1
        !else
            set-variable #l0 &spr %fonts &lget %font-sizes %font-index 
            change-font #l0
            !if $status
                100 ml-write &cat "current font: " #l0
                set-registry &spr "/history/%s/font" $platform #l0
                ; TODO: How to place this in user-setup font field???
            !else
                500 ml-write "Font is unchanged!"
            !endif
        !endif
    !emacro
    define-macro change-font-courier
        set-variable #l2 0
        ; current font is medium
        !if &sin "medium" %fonts
            set-variable #l2 1
        !endif
        set-variable %fonts %font-default
        set-variable %font-sizes  "|10|12|14|17|20|24|25|34|"
        set-variable #l0 &lget %font-sizes %font-index
        set-variable #l0 &spr %fonts #l0
        !if #l2
            set-variable #l0 &rep #l0 "bold" "medium"
        !endif
        1500 ml-write &cat "current font: " #l0
        change-font #l0
        set-registry &spr "/history/%s/font" $platform #l0
    !emacro
    define-macro change-font-lucida
        set-variable #l0 0
        ; current font is medium
        !if &sin "medium" %fonts
            set-variable #l0 1
        !endif
        set-variable %fonts "-*-lucidatypewriter-bold-*-*-*-%d-*-*-*-*-*-*-*"
        set-variable %font-sizes  "|10|12|14|17|20|24|25|34|"
        !if #l0
            set-variable %fonts &rep %fonts "bold" "medium"
        !endif
        set-variable #l0 &spr %fonts &lget %font-sizes %font-index 
        change-font #l0
        set-registry &spr "/history/%s/font" $platform #l0        
    !emacro
    define-macro change-font-type
        set-variable %fonts &spr "-*-%s-*-*-*-%%d-*-*-*-*-*-*-*" @1
        set-variable %font-sizes @2
        set-variable %font-index &lfind %font-sizes @3
        set-variable #l0 &spr %fonts @3
        !force change-font #l0
        !if &not $status 
            1500 ml-write &spr "Error: Install X11 font %s first!" @1
        !else
            ml-write &cat "Font: " &reg  &spr "/history/%s/font" $platform ""
            set-registry &spr "/history/%s/font" $platform #l0        
        !endif
        
    !emacro
    define-macro change-font-bold
        !if &sin "medium" %fonts
            set-variable %fonts &rep %fonts "medium" "bold"
        !else
            set-variable %fonts &rep %fonts "bold" "medium"
        !endif
        set-variable #l0 &spr %fonts &lget %font-sizes %font-index 
        change-font #l0
        set-registry &spr "/history/%s/font" $platform #l0
    !emacro
    1 global-bind-key change-font-size "C-+"
    0 global-bind-key change-font-size "C-="
    -1 global-bind-key change-font-size "C--"
!endif
;
; setup the next-error stuff including grep and compiling
;
set-variable $line-template "[0-9]+"
set-variable $file-template "[!-9;-z~]+"
set-variable %ipipe-term "TERM=vt100-nam"
; Grep stuff
set-variable %xgrep-com "grep -n"
; RCS stuff
set-variable $rcs-file     "RCS/%f,v"
set-variable $rcs-co-com   "co %f"
set-variable $rcs-cou-com  "co -l %f"
set-variable $rcs-ci-com   "ci -u -m\"%m\" %f"
;set-variable $rcs-cif-com  "ci -u -m\"%m\" %f"
; Declare the .dir file found in X11 font directory, this is not an info file.
add-file-hook ".dir"                                          fhook-txt
