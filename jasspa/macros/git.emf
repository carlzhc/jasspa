; This is part of the JASSPA MicroEmacs macro files
; Copyright (C) 2001-2024 JASSPA (www.jasspa.com)
;               2021-2024 D.Groth (github.com/mittelmark/microemacs)
;
; See the file me.emf for copying and conditions.
;
; Created:     Tue Aug 13 2024
; Synopsis:    Basic Git Command support macros for MicroEmacs
; Authors:     Detlef Groth
;

; add a file to git

; just a place to store some variables
0 define-macro git
    ;set-variable .lang "LANG=en_US"
!emacro    
set-variable .git.lang "LANG=en_US"
; the actual functions alphabetically ordered
; more complicated things like git clone, git branch,  git mv, git pull, git push etc
; should be done from the terminal
define-macro git-lang
    ; changing the language does break the functions below
    ; so it might be better to hide this function?
    set-variable .git.lang &cat "LANG=" @1
    !if &band $system 0x01
        screen-update
    !endif
!emacro
define-macro git-add
    !if &bmod "edit"
        set-variable #l0 @mc5 "Save buffer first (?/y/n) ? " "nNyY" "(Y)es, (N)o, (C-g)Abort ? "
        !if &iseq #l0 "y" 
            save-buffer 
        !endif
    !endif 
    2 pipe-shell-command &cat .git.lang &cat " git add " $buffer-fname "*git*"
    ml-write &spr "File %s was added. Use git-commit to complete addition to repository!" $buffer-fname
    !if &band $system 0x01
        screen-update
    !endif
!emacro
; git-branch+*~
0 define-macro git-branches-get
    2 pipe-shell-command &cat .git.lang " git branch" "*git*"
    find-buffer "*git*"
    set-variable $window-line 3
    set-variable .git.branches "|"
    
    !force forward-line
    !while $status
        set-variable #l1 @wl
        ;1000 ml-write #l1
        !if &equ &sin "*" #l1 1
            ;1000 ml-write &cat "found: " #l1
            set-variable .git.branches &linsert .git.branches 0 #l1
        !elif &equ &sin "  " #l1 1
            set-variable .git.branches &linsert .git.branches -1 &right #l1 2
        !endif
        !force forward-line
    !done
    delete-buffer "*git*"
!emacro
define-macro git-branch
    git-branches-get
    set-variable #l3 &right .git.branches 3
    set-variable #l4 &sub &len #l3 1
    set-variable #l3 &left #l3 #l4
    1000 ml-write &spr "on branch: %s (%s)" &lget .git.branches 1 #l3
!emacro

define-macro git-checkout
    git-branches-get
    !if &gre &llen .git.branches 1
        set-variable #l2 @ml19 "select branch (C-g - quit)" &lget .git.branches 1 .git.branches
        !if &not &seq #l2 &lget .git-branches 1
            2 pipe-shell-command &cat &cat .git.lang " git checkout " #l2 "*git*"
            delete-buffer "*git*"
            1000 ml-write &spr "Switched to branch: %s" #l2
        !endif
    !else
        1000 ml-write &cat "there is only one branch: " &lget .git.branches 1
    !endif
!emacro
; commit changes of the file to git repo
define-macro git-commit
    !if &bmod "edit"
        set-variable #l0 @mc5 "Save buffer first (?/y/n) ? " "nNyY" "(Y)es, (N)o, (C-g)Abort ? "
        !if &iseq #l0 "y" 
            save-buffer 
        !endif
    !endif 
    set-position "\x90"
    git-get-status
    !if &or &seq .git.status "modified" &seq .git.status "new file"
        set-variable #l0 @ml08 "Enter your commit message"
        2 pipe-shell-command &cat .git.lang &cat &cat &cat " git commit -m '" #l0 "' " $buffer-fname "*git*"
        find-buffer "*git*"
        beginning-of-buffer
        search-buffer "me" "^ 1 file changed"
        !if $status
            set-variable  #l0 &trb @wl
            delete-buffer "*git*"
            goto-position "\x90"
            1000 ml-write #l0
        !else
            delete-buffer "*git*"
            goto-position "\x90"
            1000 ml-write "Error!"
        !endif
        !if &band $system 0x01
            screen-update
        !endif
    !elif &seq .git.status "untracked"
        1000 ml-write "File is not added to Git repositoriy, add first!"
    !elif &seq .git.status "nothing to commit"
        1000 ml-write "File is commited already, nothing to commit!"
    !else
        1000 ml-write "Error: Unknown problem!"
    !endif
!emacro

add-file-hook "*git-diff*" fhook-diff

define-macro git-diff
    !if &bmod "edit"
        set-variable #l0 @mc5 "Save buffer first (?/y/n) ? " "nNyY" "(Y)es, (N)o, (C-g)Abort ? "
        !if &iseq #l0 "y"
            save-buffer
        !endif
    !endif
    0 pipe-shell-command &cat .git.lang &cat  " git diff "  $buffer-fname "*git-diff*"
    !if &band $system 0x01
        screen-update
    !endif
!emacro    
!if &not &exi dirlst-sort-list
    0 exec-file "find"
    add-file-hook "*git-grep*" fhook-grep
!endif
define-macro git-grep
    !if &bmod "edit"
        set-variable #l0 @mc5 "Save buffer first (?/y/n) ? " "nNyY" "(Y)es, (N)o, (C-g)Abort ? "
        !if &iseq #l0 "y"
            save-buffer
        !endif
    !endif
    set-variable #l0 @ml00 "git-grep with"
    0 pipe-shell-command &cat .git.lang &cat &cat " git grep -nE '"  #l0 "'" "*git-grep*"
    find-buffer "*git-grep*"
    1 buffer-mode "indent"    
    1 buffer-mode "time"        
    1 buffer-init "grep"
    ;set-variable :mouse-word-select "mouse-get-next-line"
    ;set-variable :mouse-pick-1 dirlst-mouse-pick-1
    set-variable :mouse-pick-1 git-grep-pick 
    set-variable :mouse-drop-1 git-grep-drop
    -1 buffer-mode "view"
    1 buffer-mode "edit"
    replace-string "^cd " "Directory:  "
    replace-string "^LANG=" "Grep: LANG="
    set-variable $window-line 3
!emacro
0 define-macro git-grep-drop
    beginning-of-line
    set-variable #l0 &sub &sin ":" @wl 1
    !if &gre #l0 0
        set-variable #l1 &sin ":" @wl 
        set-variable #l2 &left @wl #l0 ; fname
        set-variable #l3 &right @wl #l1
        set-variable #l4 &sub &sin ":" #l3 1
        set-variable #l3 &left #l3 #l4 ; line-number
        next-window-find-file #l2
        set-variable $window-line #l3
    !endif
!emacro
0 define-macro git-grep-pick
    ;1000 ml-write "picking git diff 2"
!emacro
define-macro git-log
    0 pipe-shell-command &cat .git.lang &cat  " git log "  $buffer-fname "*git*"
    !if &band $system 0x01
        screen-update
    !endif
!emacro    

;set-variable $debug 0
0 define-macro git-get-status
    !if &bmod "edit"
        set-variable #l0 @mc5 "Save buffer first (?/y/n) ? " "nNyY" "(Y)es, (N)o, (C-g)Abort ? "
        !if &iseq #l0 "y" 
            save-buffer 
        !endif
    !endif 
    set-position "\x90"
    2 pipe-shell-command &cat .git.lang  &cat  " git status "  $buffer-fname "*git*"
    find-buffer "*git*"
    beginning-of-buffer
    search-buffer "me" "^\\(\tmodified:\\|nothing to commit\\|Untracked files\\|\tnew file:\\|fatal: not a git repository\\)"
    ;set-variable $debug 2
    !if $status
        set-variable  #l0 &trb @wl
        delete-buffer "*git*"
        !if &sin "nothing to commit" #l0
            set-variable .git.status "nothing to commit"
        !elif &sin "modified" #l0
            set-variable .git.status "modified"
        !elif &sin "new file" #l0
            set-variable .git.status "new file"
        !elif &sin "fatal:" #l0
            set-variable .git.status "not in a git repository"
        !else
            set-variable .git.status "untracked"
        !endif
    !else
        set-variable .git.status "unknown"
    !endif
    goto-position "\x90"
!emacro    
; rename a file 
define-macro git-mv
    set-variable #l0 @ml01 "Enter new filename"
    set-variable #l1 $buffer-bname
    !if &not &seq #l0 $buffer-fname
        2 pipe-shell-command &spr "%s git mv %s %s " .git.lang   #l1 #l0 "*git*"
        delete-buffer #l1 
        read-file #l0
    !else
        ml-write "Error: Not given a new file name!"
    !endif
!emacro
; fetch and update current branch
define-macro git-pull
    0 pipe-shell-command &cat .git.lang " git pull" "*git*"
    !if &band $system 0x01
        screen-update
    !endif
!emacro    

define-macro git-rm
    !if &bmod "edit"
        set-variable #l0 @mc5 "Save buffer first (?/y/n) ? " "nNyY" "(Y)es, (N)o, (C-g)Abort ? "
        !if &iseq #l0 "y" 
            save-buffer 
        !endif
    !endif 
    git-get-status
    !if &seq .git.status "modified"
        1000 ml-write "Uncommitted changes, commit them first, then use git-rm!"
    !elif &seq .git.status "not in a git repository"
        1000 ml-write "This file is not in folder of a Git repository!"
    !elif &seq .git.status  "untracked"
        1000 ml-write "This file is not in the Git repository in the current branch!"
    !elif &seq .git.status  "new file"
        set-variable #l0 @mc5 "Remove file from Git (?/y/n) ? " "nNyY" "(Y)es, (N)o, (C-g)Abort ? "
        !if &iseq #l0 "y" 
            2 pipe-shell-command &spr  "%s git rm -f %s" .git.lang $buffer-fname "*git*"
            1000 ml-write "file was removed from the current branch"
            delete-buffer
        !endif
    !elif &seq .git.status "nothing to commit"
        set-variable #l0 @mc5 "Remove file from Git (?/y/n) ? " "nNyY" "(Y)es, (N)o, (C-g)Abort ? "
        !if &iseq #l0 "y" 
            2 pipe-shell-command &spr  "%s git rm -f %s" .git.lang $buffer-fname "*git*"
            1000 ml-write "file was removed from the current branch"
            delete-buffer
        !endif
    !else
!emacro
define-macro git-status
    !if &bmod "edit"
        set-variable #l0 @mc5 "Save buffer first (?/y/n) ? " "nNyY" "(Y)es, (N)o, (C-g)Abort ? "
        !if &iseq #l0 "y" 
            save-buffer 
        !endif
    !endif 
    git-get-status
    1000 ml-write .git.status
    !if &band $system 0x01
        screen-update
    !endif

!emacro    

define-macro git-state
    0 pipe-shell-command &cat .git.lang " git status ." "*git*"
    !if &band $system 0x01
        screen-update
    !endif
!emacro    

define-macro git-tag
    !if &bmod "edit"
        set-variable #l0 @mc5 "Save buffer first (?/y/n) ? " "nNyY" "(Y)es, (N)o, (C-g)Abort ? "
        !if &iseq #l0 "y"
            save-buffer
        !endif
    !endif
    !if &and @? &equ @# 1
        set-variable #l0 @ml08 "enter tagname (like v1.0-beta0)"
        set-variable #l1 @ml08 "enter message (like v1.0-beta0)"
        0 pipe-shell-command &cat .git.lang &spr " git tag -a '%s' -m '%s'" #l0 #l1 "*git*"
    !else
        0 pipe-shell-command &cat .git.lang " git tag" "*git*"
        !if &band $system 0x01
            screen-update
        !endif
    !endif
!emacro
